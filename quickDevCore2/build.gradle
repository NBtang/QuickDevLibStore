apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


android {
    compileSdkVersion build_version.compileSdkVersion

    defaultConfig {
        minSdkVersion build_version.minSdkVersion
        targetSdkVersion build_version.targetSdkVersion
        versionCode build_version.versionCode
        versionName build_version.versionName

        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    api libs.annotations
    api (libs.support_v7){
        exclude group: 'androidx.annotation'
    }

    api libs.kotlin_stdlib
    api libs.kotlin_coroutines //协程

    api libs.jetpack_lifecycle_extensions
    api libs.jetpack_lifecycle_java8
    kapt libs.jetpack_lifecycle_compiler

    api libs.jetpack_viewmodel
    api libs.jetpack_livedata
    api libs.jetpack_livedata_rx

    // retrofit
    api (libs.retrofit){
        exclude module: 'okhttp'
        exclude module: 'okio'
    }
    api (libs.retrofit_gson){
        exclude module: 'gson'
        exclude module: 'okhttp'
        exclude module: 'okio'
        exclude module: 'retrofit'
    }
    api (libs.retrofit_rxjava){
        exclude module: 'rxjava'
        exclude module: 'okhttp'
        exclude module: 'retrofit'
        exclude module: 'okio'
    }
    api ('com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'){
        exclude module: 'retrofit'
        exclude group: 'org.jetbrains.kotlinx'
    }


    // rx
    api libs.rxkotlin
    api (libs.rxandroid){
        exclude module: 'rxjava'
    }
    api libs.rxrelay
    api libs.rxerrorhandler2

    api (libs.rxcache2) {
        exclude module: 'rxjava'
    }
    implementation (libs.rxcache_jolyglot_gson) {
        exclude module: 'gson'
    }
    api(libs.rxbinding) {
        exclude module: 'rxjava'
    }

    // autodispose
    api (libs.autodispose){
        exclude module: 'rxjava'
    }

    api (libs.autodispose_archs_ktx){
        exclude module: 'rxjava'
        exclude module: 'rxandroid'
    }

    // kodein
    api libs.kodein
    api libs.kodein_android
    api (libs.kodein_androidx){
        exclude group: 'androidx.appcompat'
    }

    // okhttp
    api libs.okhttp
    api libs.okhttp_logging

    api libs.gson

    // logger
    api libs.timber
    api libs.logger

    api libs.koi_core
    api libs.koi_async

    implementation libs.retrofit_url_manager

    api 'com.github.GrenderG:Toasty:1.4.0'
    api 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    api 'com.laotang.quickdev:localretrofit:1.0.3'
    api 'com.laotang.quickdev:rx-activity-result:1.0.0'
    api 'com.laotang.quickdev:eventsLogger:1.0.0'
}

ext {
    //bintray 网站上你创建的仓库的名字（必配项）
    bintrayRepo = 'maven'
    //在这个仓库下的 package name（必配项）
    bintrayName = 'core2'

    publishedGroupId = 'com.laotang.quickdev'
    artifact = 'core2'
    libraryVersion = '1.0.10'

    libraryName = 'core2'
    libraryDescription = ''
    siteUrl = ''
    gitUrl = ''

    //开发者信息，也是必配的吧
    developerId = 'laotang'
    developerName = 'laotang'
    developerEmail = 'xiaoyaobeibeizhu@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply plugin: 'com.github.dcendents.android-maven'

group = publishedGroupId                               // Maven Group ID for the artifact

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact

                // Add your description here
                name libraryName
                description libraryDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task generateSourcesJar(type: Jar) {
    group = 'jar'
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

//task sourcesJar(type: Jar) {
//    classifier = 'sources'
//    from android.sourceSets.main.java.srcDirs
//}

task javadoc(type: Javadoc) {
    options.encoding="utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

//task javadocJar(type: Jar, dependsOn: javadoc) {
//    classifier = 'javadoc'
//    from javadoc.destinationDir
//}

//同时添加三个方法
//task generateSourcesJar(type: Jar) {
//    group = 'jar'
//    from android.sourceSets.main.java.srcDirs
//    classifier = 'sources'
//}
task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = "javadoc"
    outputDirectory = javadoc.destinationDir
}
task generateJavadoc(type: Jar, dependsOn: dokkaJavadoc) {
    group = 'jar'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
//    archives javadocJar
//    archives sourcesJar
    archives generateJavadoc //javadocJar
    archives generateSourcesJar //sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        userOrg = 'nblaotang'
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = properties.getProperty("bintray.gpg.password")
                //Optional. The passphrase for GPG signing'
            }
        }
    }
}
